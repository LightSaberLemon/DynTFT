{   DynTFT  - graphic components for microcontrollers
    Copyright (C) 2017 VCC
    release date: 29 Dec 2017
    author: VCC

    This file is part of DynTFT project.

    DynTFT is free software: you can redistribute it and/or modify
    it under the terms of the GNU Lesser General Public License as published by
    the Free Software Foundation, version 3 of the License.

    DynTFT is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Lesser General Public License for more details.

    You should have received a copy of the GNU Lesser General Public License
    along with DynTFT, in COPYING.LESSER file.
    If not, see <http://www.gnu.org/licenses/>.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
    DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
    TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
    OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}

unit DynTFTListExporter;

interface

procedure GenerateListFile(AFileName: string);


implementation

uses
  SysUtils, Classes, DynTFTGUIObjects, DynTFTHandlers, DynTFTFonts;


function GetFontAddress(IdentifierName: string; PointerHexByteSize: Integer; PointerSizeStr: string): string;
var
  i: Integer;
begin
  Result := '0x00000000 [0] ' + IdentifierName + ' ;unknown_address';
  for i := 0 to Length(CAllFontSettings) - 2 do  // -2, because CAllFontSettings starts at index -1
    if '_' + CAllFontSettings[i].IdentifierName = IdentifierName then
    begin
      Result := '0x' + IntToHex(Int64(@CAllFontSettings[i]), PointerHexByteSize) + PointerSizeStr + '_' + CAllFontSettings[i].IdentifierName;
      Exit;
    end;
end;

  
procedure GenerateListFile(AFileName: string);
var
  AStringList: TStringList;
  i: Integer;
  PointerHexByteSize: Integer;
  PointerSizeStr: string;
begin
  AStringList := TStringList.Create;
  try
    AStringList.Add(';  LST file generated by DynTFTListExporter');
    AStringList.Add('; Date/Time: ' + DateTimeToStr(Now));
    AStringList.Add(';----------------------------------------------');
    AStringList.Add('');
    AStringList.Add('Symbol List:');
    AStringList.Add('//** Routines locations **');
    AStringList.Add('//ADDRESS    SIZE    PROCEDURE');
    AStringList.Add('//----------------------------------------------');

    PointerHexByteSize := SizeOf(Pointer) shl 1;

    PointerSizeStr := ' [' + IntToStr(SizeOf(Pointer)) + '] ';

    UpdateAllBinHandlerStrArray;

    for i := 0 to Length(AllBinHandlersStr) - 1 do
      AStringList.Add('0x' + IntToHex(Int64(AllBinHandlersAddresses[i]), PointerHexByteSize) + PointerSizeStr + AllBinHandlersStr[i]);

    AStringList.Add('//** Variables locations ** ');
    AStringList.Add('//ADDRESS    SIZE    VARIABLE');
    AStringList.Add('//----------------------------------------------');

    for i := 0 to Length(CAllCreatedBinComponents) - 1 do
      AStringList.Add('0x' + IntToHex(Int64(CAllCreatedBinComponents[i]), PointerHexByteSize) + PointerSizeStr + 'Component[' + IntToStr(i) + ']');

    AStringList.Add('//** Constants locations ** ');
    AStringList.Add('//ADDRESS    SIZE    CONSTANT');
    AStringList.Add('//----------------------------------------------');

    for i := 0 to Length(AllBinIdentifiersStr) - 1 do
      AStringList.Add(GetFontAddress(AllBinIdentifiersStr[i], PointerHexByteSize, PointerSizeStr));  

    AStringList.SaveToFile(AFileName);
  finally
    AStringList.Free;
  end;
end;


end.
